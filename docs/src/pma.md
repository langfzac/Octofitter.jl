# [Fit Proper Motion Anomaly](@id fit-pma)

One of the features of Octofitter.jl is support for absolute astrometry aka. proper motion anomaly aka. astrometric acceleration.
These data points are typically calculated by finding the difference between a long term proper motion of a star between the Hipparcos and GAIA catalogs, and their proper motion calculated within the windows of each catalog.

For Hipparcos/GAIA this gives four data points that can constrain the dynamical mass & orbits of planetary companions (assuming we subtract out the net trend).

You can specify these quantities manually, but the easiest way is to use the Hipparcos-GAIA Catalog of Accelerations (HGCA, [https://arxiv.org/abs/2105.11662](https://arxiv.org/abs/2105.11662)). Support for loading this catalog is built into Octofitter.jl.

Let's look at the star and companion [HD 91312 A & B](https://arxiv.org/abs/2109.12124), discovered by SCExAO. We will use their published astrometry and proper motion anomaly extracted from the HGCA.

The first step is to find the GAIA source ID for your object. For HD 91312, SIMBAD tells us the GAIA DR2 ID is `756291174721509376` (which we will assume is the same in eDR3).

## Planet Model
For this model, we will have to add the variable `mass` as a prior.
The units used on this variable are Jupiter masses, in contrast to `M`, the primary's mass, in solar masses.  A reasonable uninformative prior for `mass` is `Uniform(0,1000)` or `LogUniform(1,1000)` depending on the situation.

Initial setup:
```@example 1
using Octofitter, Distributions, Plots
```


```@example 1
astrom = PlanetRelAstromLikelihood(
    (epoch=mjd("2016-12-15"), ra=133., dec=-174., σ_ra=07.0, σ_dec=07., cor=0.2),
    (epoch=mjd("2017-03-12"), ra=126., dec=-176., σ_ra=04.0, σ_dec=04., cor=0.3),
    (epoch=mjd("2017-03-13"), ra=127., dec=-172., σ_ra=04.0, σ_dec=04., cor=0.1),
    (epoch=mjd("2018-02-08"), ra=083., dec=-133., σ_ra=10.0, σ_dec=10., cor=0.4),
    (epoch=mjd("2018-11-28"), ra=058., dec=-122., σ_ra=10.0, σ_dec=20., cor=0.3),
    (epoch=mjd("2018-12-15"), ra=056., dec=-104., σ_ra=08.0, σ_dec=08., cor=0.2),
)
@planet B Visual{KepOrbit} begin
    a ~ truncated(LogNormal(9,3),lower=0)
    e ~ truncated(Normal(0, 0.5), lower=0, upper=1) #Uniform(0,0.9999)
    ω ~ UniformCircular()
    i ~ Sine() # The Sine() distribution is defined by Octofitter
    Ω ~ UniformCircular()
    # mass ~ LogNormal(300, 18)
    # Anoter option would be:
    mass ~ Uniform(0.5, 1000)


    τ ~ UniformCircular(1.0)
    P = √(B.a^3/system.M)
    tp =  B.τ*B.P*365.25 + 57737 # reference epoch for τ. Choose an MJD date near your data.
end astrom
```


## System Model & Specifying Proper Motion Anomaly
Now that we have our planet model, we create a system model to contain it.

```@example 1
@system HD91312 begin
    M ~ truncated(Normal(1.61, 0.1), lower=0)
    plx ~ gaia_plx(gaia_id=756291174721509376)
            
    # Priors on the centre of mass proper motion
    pmra ~ Normal(-137, 10)
    pmdec ~ Normal(2,  10)
end HGCALikelihood(gaia_id=756291174721509376) B
```

We specify priors on `M` and `plx` as usual, but here we use the `gaia_plx` helper function to read the parallax and uncertainty directly from the HGCA using its source ID.

We also add parameters for the long term system's proper motion. This is usually
close to the long term trend between the Hipparcos and GAIA measurements.

After the priors, we add the proper motion anomaly measurements from the HGCA. If this is your first time running this code, you will be prompted to automatically download and cache the catalog which may take around 30 seconds.


## Sampling from the posterior
Sample from our model as usual:

```@example 1
model = Octofitter.LogDensityModel(HD91312)
chain = octofit(model)
```

This takes about a minute on the first run due to JIT startup latency; subsequent runs are very quick even on e.g. an older laptop.

## Analysis

The first step is to look at the table output above generated by MCMCChains.jl.
The `rhat` column gives a convergence measure. Each parameter should have an `rhat` very close to 1.000.
If not, you may need to run the model for more iterations or tweak the parameterization of the model to improve sampling.
The `ess` column gives an estimate of the effective sample size.
The `mean` and `std` columns give the mean and standard deviation of each parameter.

The second table summarizes the 2.5, 25, 50, 75, and 97.5 percentiles of each parameter in the model.

Another useful plotting function is `octoplot` which takes similar arguments and produces a 9 panel plot:
```@example 1
octoplot(model, chain)
```


### Pair Plot
If we wish to examine the covariance between parameters in more detail, we can construct a pair-plot (aka. corner plot).

For a quick look, you can just run `octocorner(model, chain)`, but for more control output you may wish to customize the labels, units, unit labels, etc. See the documentation of `PairPlots.jl` for more details.


```@example 1
# Create a corner plot / pair plot.
# We can access any property from the chain specified in Variables
using CairoMakie: Makie
using PairPlots
octocorner(model, chain, small=true)
```


## Fitting Astrometric Acceleration Only
If you wish to look at the possible locations/masses of a planet around a star using onnly GAIA/HIPPARCOS,
you can follow a simplified approach.

As a start, you can restrict the orbital parameters to just semi-major axis, epoch of periastron passage, and mass.

```@example 1
@planet B Visual{KepOrbit} begin
    a ~ LogUniform(0.1, 100)
    mass ~ LogUniform(1, 2000)
    i = 0
    e = 0
    Ω = 0
    ω = 0
      τ ~ UniformCircular(1.0)
    P = √(B.a^3/system.M)
    tp =  B.τ*B.P*365.25 + 57737 # reference epoch for τ. Choose an MJD date near your data.
end
@system HD91312 begin
    M ~ truncated(Normal(1.61, 0.2), lower=0)
    plx ~ gaia_plx(gaia_id=756291174721509376)
    
    # Priors on the centre of mass proper motion
    pmra ~ Normal(0, 500)
    pmdec ~ Normal(0,  500)
end HGCALikelihood(gaia_id=756291174721509376) B
```

This models assumes a circular, face-on orbit.

```@example 1
model = Octofitter.LogDensityModel(HD91312; autodiff=:ForwardDiff, verbosity=4) # defaults are ForwardDiff, and verbosity=0

chain = octofit(model)
```

With such simple models, the mean tree depth is often very low and sampling proceeds very quickly.

A good place to start is a histogram of planet mass vs. semi-major axis:
```@example 1
Plots.histogram2d(chain["B_a"], chain["B_mass"], color=:plasma, xguide="sma (au)", yguide="mass (Mjup)")
```


You can also visualize the orbits and proper motion:
```@example 1
octoplot(model, chain)
```
